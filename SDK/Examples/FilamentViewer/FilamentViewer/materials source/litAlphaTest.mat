// This material is for use with texture having an alpha channel, where we
// want a fragment to be discarded if the alpha is below a threshold, and
// opaque otherwise.

material {
	name : litAlphaTest,
	shadingModel : lit,
    doubleSided : false,
	postLightingBlending : add,
	specularAntiAliasing : true,
    requires : [
        uv0
    ],
	parameters : [
        {
            type : bool,
            name : isTextured
        },
		{
            type : bool,
            name : isSpecularMapped
		},
        {
            type : float,
            name : alphaThreshold
        },
        {
            type : float3,
            name : baseColor
        },
		{
            type : sampler2d,
            name : texture
		},
		{
            type : sampler2d,
            name : specularMap
		},
        {
            type : float,
            name : roughness
        },
        {
            type : float,
            name : metallic
        },
        {
            type : float,
            name : reflectance
        },
		{
			type : float3,
			name : emissive
		},
		{
			type : float3x3,
			name : uvtrans
		},
		{
			type : float2,
			name : depthRange
		}
	]
}

vertex {
	void materialVertex(inout MaterialVertexInputs material)
	{
		// No idea why I must do this song and dance to get UV transforms
		// to work correctly.  It may have something to do with the V flipping
		// that Filament normally does, but if I turn off flipUV, the results
		// are worse.
		material.uv0.y = 1.0 - material.uv0.y;
		float3 uvhom = materialParams.uvtrans * float3( material.uv0, 1.0 );
		material.uv0 = uvhom.xy;
		material.uv0.y = 1.0 - material.uv0.y;
		
		// glDepthRange emulation
		material.clipSpaceTransform[3][2] = materialParams.depthRange.x +
			materialParams.depthRange.y - 1.0;
		material.clipSpaceTransform[2][2] = materialParams.depthRange.y -
			materialParams.depthRange.x;
	}
}

fragment {
	void material(inout MaterialInputs material) {
		prepareMaterial(material);

		material.baseColor.a = 1.0;
		float2 uv = getUV0();
		float alpha = 1.0;
		if (materialParams.isTextured)
		{
			float4 textureColor = texture(materialParams_texture, uv).rgba;
			alpha = textureColor.a;
			unpremultiply( textureColor );
			material.baseColor.rgb = textureColor.rgb;
		}
		else
		{
			material.baseColor.rgb = materialParams.baseColor;
		}
		if (alpha < materialParams.alphaThreshold)
		{
			discard;
		}
		if (materialParams.isSpecularMapped)
		{
			vec4 specVal = texture(materialParams_specularMap, uv);
			material.reflectance = (specVal.r + specVal.g + specVal.b) / 3.0;
			material.roughness = (1.0 - specVal.a) / (1.0 + 4.0 * specVal.a);
		}
		else
		{
			material.roughness = materialParams.roughness;
			material.reflectance = materialParams.reflectance;
		}
		material.metallic = materialParams.metallic;
		material.postLightingColor.rgb = materialParams.emissive;
		material.postLightingColor.a = 0.0;
	}
}
