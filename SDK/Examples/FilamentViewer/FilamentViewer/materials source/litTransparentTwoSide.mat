material {
	name : litTransparent,
	shadingModel : lit,
	blending : transparent,
	transparency : twoPassesTwoSides,
    doubleSided : true,
	postLightingBlending : add,
    requires : [
        uv0
    ],
	parameters : [
        {
            type : bool,
            name : isTextured
        },
		{
            type : bool,
            name : isSpecularMapped
		},
        {
            type : float,
            name : alpha
        },
        {
            type : float3,
            name : baseColor
        },
		{
            type : sampler2d,
            name : texture
		},
		{
            type : sampler2d,
            name : specularMap
		},
        {
            type : float,
            name : roughness
        },
        {
            type : float,
            name : metallic
        },
        {
            type : float,
            name : reflectance
        },
		{
			type : float3,
			name : emissive
		},
		{
			type : float3x3,
			name : uvtrans
		},
		{
			type : float2,
			name : depthRange
		}
	]
}

vertex {
	void materialVertex(inout MaterialVertexInputs material)
	{
		// No idea why I must do this song and dance to get UV transforms
		// to work correctly.  It may have something to do with the V flipping
		// that Filament normally does, but if I turn off flipUV, the results
		// are worse.
		material.uv0.y = 1.0 - material.uv0.y;
		float3 uvhom = materialParams.uvtrans * float3( material.uv0, 1.0 );
		material.uv0 = uvhom.xy;
		material.uv0.y = 1.0 - material.uv0.y;
		
		// glDepthRange emulation
		material.clipSpaceTransform[3][2] = materialParams.depthRange.x +
			materialParams.depthRange.y - 1.0;
		material.clipSpaceTransform[2][2] = materialParams.depthRange.y -
			materialParams.depthRange.x;
	}
}

fragment {
	void material(inout MaterialInputs material) {
		prepareMaterial(material);
		if (materialParams.isTextured)
		{
			material.baseColor.rgba = texture(materialParams_texture, getUV0()).rgba *
				materialParams.alpha;
		}
		else
		{
			material.baseColor.rgb = materialParams.baseColor * materialParams.alpha;
			material.baseColor.a = materialParams.alpha;
		}
		if (materialParams.isSpecularMapped)
		{
			vec4 specVal = texture(materialParams_specularMap, getUV0());
			material.reflectance = (specVal.r + specVal.g + specVal.b) / 3.0;
			material.roughness = (1.0 - specVal.a) / (1.0 + 4.0 * specVal.a);
		}
		else
		{
			material.roughness = materialParams.roughness;
			material.reflectance = materialParams.reflectance;
		}
		material.metallic = materialParams.metallic;
		material.postLightingColor.rgb = materialParams.emissive;
		material.postLightingColor.a = 0.0;
	}
}
